import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Services.Pam
import Quickshell.Wayland
import qs.Commons

Item {
  id: root

  property var shell: null
  property string omarchyPath: ""

  readonly property string home: Quickshell.env("HOME")
  readonly property string stateHome: home + "/.local/state"
  readonly property string userName: Quickshell.env("USER") || Quickshell.env("LOGNAME")
  readonly property string currentBackgroundLink: stateHome + "/omarchy/current/background"

  property bool lockRequested: false
  property bool pendingSessionLock: false
  property bool authenticatingPassword: false
  property bool fingerprintAuthenticating: false
  property bool passwordPamConfigured: false
  property bool fingerprintConfigured: false
  property bool previewVisible: false
  property string enteredPassword: ""
  property string pendingPassword: ""
  property string failureMessage: ""
  property int failedAttempts: 0
  property string backgroundPath: ""
  property int backgroundVersion: 0
  property string lastEvent: "init"
  property string lastEventAt: ""
  property bool strandedLock: false
  property bool strandedLockResolved: false
  // Face unlock: a PAM module ahead of the password one (pam_glance, howdy)
  // announces itself with an info message before it scans, and the
  // conversation only reaches the password prompt if that scan failed. That
  // is enough to drive the indicator without knowing anything about the
  // module. hidden | scanning | success | failure.
  property string faceState: "hidden"
  property string faceMessage: ""
  property bool faceAfterglow: false
  property string faceAfterglowPhase: "hidden"
  readonly property string faceMessagePrefix: "Glance:"
  // A scan can finish in a few hundred milliseconds, before the pill has even
  // arrived. Shown as it happens, that is a flash: no mark, no ring, a tick
  // that is gone before it is read. A success is held back until this much
  // time has passed since the scan began — enough for the slide, the growth,
  // the mark's beat and a moment of the ring. It costs a fast scan under a
  // second and costs a slow one nothing. Set to 0 to unlock the instant PAM
  // answers.
  readonly property int faceMinScanMs: 1400
  property double faceScanStartedAt: 0

  readonly property bool locked: lockRequested || sessionLock.locked || sessionLock.secure
  readonly property bool authenticating: authenticatingPassword || fingerprintAuthenticating

  function realScreenCount() {
    var screens = Quickshell.screens || []
    var count = 0

    for (var i = 0; i < screens.length; i++) {
      var screen = screens[i]
      if (screen && screen.name && screen.width > 0 && screen.height > 0) count += 1
    }

    return count
  }

  function hasRealScreen() {
    return realScreenCount() > 0
  }

  function queueSessionLock() {
    pendingSessionLock = true
    if (!sessionLockStabilizeTimer.running) logEvent("lock-pending: screen-stabilizing")
    sessionLockStabilizeTimer.restart()
    if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
  }

  function requestSessionLock() {
    if (!lockRequested || sessionLock.locked || sessionLock.secure) return
    if (sessionLockStabilizeTimer.running) return

    if (!hasRealScreen()) {
      if (!pendingSessionLock || lastEvent !== "lock-pending: no-real-screen") logEvent("lock-pending: no-real-screen")
      pendingSessionLock = true
      if (!pendingSessionLockTimer.running) pendingSessionLockTimer.start()
      return
    }

    pendingSessionLock = false
    pendingSessionLockTimer.stop()
    sessionLock.locked = true
  }

  // ext-session-lock outlives its client, and a restart carries no lock over, so
  // a session locked this early is an orphan behind Hyprland's failsafe. Outputs
  // are often still absent here, so ask until the answer means something.
  function checkStrandedLock() {
    if (strandedLockResolved || strandedLockCheckProc.running) return

    // A lock this shell took is nobody's orphan.
    if (locked || lockRequested) {
      strandedLockResolved = true
      return
    }

    strandedLockCheckProc.running = true
  }

  function recoverStrandedLock() {
    if (!strandedLock || locked || !passwordPamConfigured) return

    strandedLock = false
    logEvent("lock-stranded: recovering")
    beginLock()
  }

  function refreshBackground() {
    if (!readlinkProc.running) readlinkProc.running = true
  }

  function refreshFingerprintStatus() {
    if (!fingerprintCheckProc.running) fingerprintCheckProc.running = true
  }

  function logEvent(event) {
    lastEvent = event
    lastEventAt = new Date().toISOString()
    console.log("omarchy lock " + lastEventAt + " " + event)
  }

  function beginFaceScan() {
    faceState = "scanning"
    faceMessage = ""
    faceScanStartedAt = Date.now()
    faceHideTimer.stop()
    faceUnlockTimer.stop()
    faceSuccessTimer.stop()
  }

  // The scan did not unlock: PAM moved on to the password prompt, or the
  // whole conversation failed. Show the miss briefly, then get out of the way.
  function faceScanMissed() {
    dropAfterglow()
    if (faceState !== "scanning") return
    faceState = "failure"
    faceHideTimer.restart()
  }

  // PAM has said yes. Let the pill finish arriving before it is told.
  function faceScanSucceeded() {
    var wait = faceMinScanMs - (Date.now() - faceScanStartedAt)
    if (wait > 0) {
      faceSuccessTimer.interval = wait
      faceSuccessTimer.restart()
      return
    }
    showFaceSuccess()
  }

  // Hold the success frame for a beat before the lock drops, or the check
  // mark is never seen.
  function showFaceSuccess() {
    faceState = "success"
    faceHideTimer.stop()
    beginAfterglow()
    faceUnlockTimer.restart()
  }

  // --- the unlock afterglow -------------------------------------------
  //
  // The lock surface is destroyed the instant the session unlocks, taking the
  // indicator with it — so the check mark would disappear at exactly the
  // moment it is doing its job, and the unlock would end on a blink. This
  // layer-shell window carries the same capsule, at the same place, across
  // that boundary: raised just before the lock drops, and left on the desktop
  // for a beat afterwards.
  //
  // It takes no keyboard focus and passes every click straight through, so it
  // cannot get between the user and the session it has just handed back.
  // Raised at the verdict, not at the unlock. A layer-shell window is not on
  // screen when it is asked for — it has to be created, configured and
  // painted, measured here at 60-230ms — while the lock surface is destroyed
  // in the same turn as the request. Raising it at the unlock therefore leaves
  // a stretch with neither on screen, which reads as the capsule vanishing and
  // popping back.
  //
  // A session lock surface renders above every layer-shell layer, so a window
  // raised now stays invisible until the lock drops and reveals it, already
  // painted and in place. Should a compositor ever show it early, what appears
  // is the same capsule at the same position, so there is still nothing to
  // see.
  function beginAfterglow() {
    faceAfterglow = true
    faceAfterglowPhase = "success"
  }

  // The beat on the desktop is measured from when the lock actually goes, not
  // from when the window was raised.
  function holdAfterglow() {
    afterglowHoldTimer.restart()
  }

  function endAfterglow() {
    afterglowHoldTimer.stop()
    faceAfterglowPhase = "hidden"   // slides up and fades
    afterglowExitTimer.restart()
  }

  // Nothing half-faded may survive into a new lock.
  function dropAfterglow() {
    afterglowHoldTimer.stop()
    afterglowExitTimer.stop()
    faceAfterglowPhase = "hidden"
    faceAfterglow = false
  }

  function clearFace() {
    faceState = "hidden"
    faceMessage = ""
    faceHideTimer.stop()
    faceUnlockTimer.stop()
    faceSuccessTimer.stop()
  }

  function handlePamMessage() {
    var text = String(passwordPam.message || "")
    if (!passwordPam.responseRequired && text.indexOf(faceMessagePrefix) === 0) {
      beginFaceScan()
    } else if (passwordPam.responseRequired) {
      faceScanMissed()
    }
    respondToPasswordPrompt()
  }

  function resetAuthenticationState() {
    enteredPassword = ""
    pendingPassword = ""
    failureMessage = ""
    failedAttempts = 0
    authenticatingPassword = false
    clearFace()
    fingerprintAuthenticating = false
    fingerprintRetryTimer.stop()
    if (passwordPam.active) passwordPam.abort()
    if (fingerprintPam.active) fingerprintPam.abort()
  }

  function beginLock() {
    if (!passwordPamConfigured) {
      logEvent("lock-denied: missing-pam")
      return false
    }

    resetAuthenticationState()
    dropAfterglow()
    lockRequested = true
    armBlankTimer()
    logEvent("lock-requested")
    queueSessionLock()

    Qt.callLater(function() {
      root.refreshBackground()
      root.refreshFingerprintStatus()
    })

    return true
  }

  function finishUnlock() {
    if (!root.locked && !lockRequested) return

    lockRequested = false
    pendingSessionLock = false
    sessionLockStabilizeTimer.stop()
    pendingSessionLockTimer.stop()
    resetAuthenticationState()
    idleBlankTimer.stop()
    sessionLock.locked = false
    logEvent("unlocked")
    runWake()
  }

  function armBlankTimer() {
    idleBlankTimer.armedAt = Date.now()
    idleBlankTimer.restart()
  }

  function runWake() {
    if (!wakeProcess.running) wakeProcess.running = true
    if (lockRequested) armBlankTimer()
  }

  function runBlank() {
    if (!blankProcess.running) blankProcess.running = true
  }

  function submitPassword(value) {
    var password = String(value || "")
    if (!lockRequested || authenticatingPassword || password.length === 0) return

    runWake()
    pendingPassword = password
    failureMessage = ""
    authenticatingPassword = true

    if (!passwordPam.start()) {
      handlePasswordFailure()
      return
    }

    Qt.callLater(respondToPasswordPrompt)
  }

  function respondToPasswordPrompt() {
    if (!authenticatingPassword || !passwordPam.active || !passwordPam.responseRequired) return
    passwordPam.respond(pendingPassword)
  }

  function handlePasswordFailure() {
    if (!lockRequested) return

    authenticatingPassword = false
    enteredPassword = ""
    pendingPassword = ""
    failedAttempts += 1
    failureMessage = "Authentication failed (" + failedAttempts + ")"
    runWake()
  }

  function startFingerprint() {
    if (!lockRequested || !sessionLock.secure || !fingerprintConfigured) return
    if (fingerprintPam.active || fingerprintAuthenticating) return

    fingerprintAuthenticating = true
    if (!fingerprintPam.start()) {
      fingerprintAuthenticating = false
    }
  }

  function handleFingerprintFinished(result) {
    fingerprintAuthenticating = false

    if (!lockRequested) return
    if (result === PamResult.Success) {
      finishUnlock()
    } else if (fingerprintConfigured) {
      fingerprintRetryTimer.restart()
    }
  }

  WlSessionLock {
    id: sessionLock

    locked: false

    onSecureStateChanged: {
      root.logEvent("secure=" + secure)
      if (secure) {
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.startFingerprint()
      }
    }

    onLockStateChanged: {
      root.logEvent("session-locked=" + locked)

      if (locked) {
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
      }

      if (!locked && root.lockRequested) {
        root.lockRequested = false
        root.pendingSessionLock = false
        sessionLockStabilizeTimer.stop()
        pendingSessionLockTimer.stop()
        root.resetAuthenticationState()
        root.runWake()
      }
    }

    WlSessionLockSurface {
      id: lockSurface
      color: Color.background

      LockView {
        id: lockView
        anchors.fill: parent
        backgroundPath: root.backgroundPath
        backgroundVersion: root.backgroundVersion
        fingerprintConfigured: root.fingerprintConfigured
        authenticatingPassword: root.authenticatingPassword
        failureMessage: root.failureMessage
        failedAttempts: root.failedAttempts
        inputEnabled: root.lockRequested
        loadBackground: root.locked
        passwordText: root.enteredPassword
        faceState: root.faceState
        faceMessage: root.faceMessage
        onPasswordTextEdited: function(password) { root.enteredPassword = password }
        onSubmitPassword: function(password) { root.submitPassword(password) }
        onClearFailureRequested: root.failureMessage = ""
        onWakeRequested: root.runWake()
      }

    }
  }

  PanelWindow {
    id: afterglowWindow
    visible: root.faceAfterglow
    anchors { top: true; bottom: true; left: true; right: true }
    color: "transparent"
    WlrLayershell.namespace: "omarchy-lock-afterglow"
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.None
    exclusionMode: ExclusionMode.Ignore
    // Empty input region: the pointer never meets this window at all.
    mask: Region {}

    FaceUnlockIndicator {
      phase: root.faceAfterglowPhase
      message: root.faceMessage
      // The same offset LockView gives it, so the capsule does not move by a
      // pixel as it changes surfaces.
      topMargin: Math.round(parent.height * 0.07)
      animateEntry: false
      overDesktop: true
    }
  }

  PanelWindow {
    id: previewWindow
    visible: root.previewVisible
    anchors { top: true; bottom: true; left: true; right: true }
    color: "transparent"
    WlrLayershell.namespace: "omarchy-lock-preview"
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
    exclusionMode: ExclusionMode.Ignore

    LockView {
      anchors.fill: parent
      backgroundPath: root.backgroundPath
      backgroundVersion: root.backgroundVersion
      fingerprintConfigured: root.fingerprintConfigured
      authenticatingPassword: false
      failureMessage: ""
      failedAttempts: 0
      inputEnabled: false
      loadBackground: root.previewVisible
      passwordText: ""
      faceState: root.faceState
      faceMessage: root.faceMessage
    }

    MouseArea {
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton
      onClicked: root.previewVisible = false
    }
  }

  PamContext {
    id: passwordPam
    config: "omarchy-lock-password"
    user: root.userName

    onResponseRequiredChanged: root.handlePamMessage()
    onPamMessage: root.handlePamMessage()

    onCompleted: function(result) {
      root.pendingPassword = ""

      if (!root.lockRequested) {
        root.authenticatingPassword = false
        return
      }
      if (result === PamResult.Success) {
        // A face success: keep "Checking…" up and the display awake while the
        // indicator shows its check mark; faceUnlockTimer finishes the unlock.
        if (root.faceState === "scanning") {
          root.faceScanSucceeded()
          return
        }
        root.authenticatingPassword = false
        root.finishUnlock()
      } else {
        root.authenticatingPassword = false
        root.faceScanMissed()
        root.handlePasswordFailure()
      }
    }

    onError: function(error) {
      root.authenticatingPassword = false
      root.faceScanMissed()
      root.handlePasswordFailure()
    }
  }

  // Long enough to read the check mark, short enough that the session does
  // not feel withheld: the afterglow carries the rest of the confirmation on
  // the far side of the unlock, where it costs the user nothing.
  Timer {
    id: faceUnlockTimer
    interval: 550
    repeat: false
    onTriggered: {
      root.authenticatingPassword = false
      if (!root.lockRequested) {
        root.dropAfterglow()
        return
      }
      root.finishUnlock()
      root.holdAfterglow()
    }
  }

  Timer {
    id: faceSuccessTimer
    repeat: false
    onTriggered: if (root.lockRequested && root.faceState === "scanning") root.showFaceSuccess()
  }

  // Long enough that the tick, which now arrives after the sphere's spin,
  // is read on the desktop rather than glimpsed.
  Timer {
    id: afterglowHoldTimer
    interval: 1200
    repeat: false
    onTriggered: root.endAfterglow()
  }

  Timer {
    id: afterglowExitTimer
    interval: 420  // covers the indicator's own exit transition
    repeat: false
    onTriggered: root.faceAfterglow = false
  }

  // Long enough that the miss registers, short enough that the pill is not
  // still sulking over a password field that has already taken focus.
  Timer {
    id: faceHideTimer
    interval: 1200
    repeat: false
    onTriggered: if (root.faceState === "failure") root.faceState = "hidden"
  }

  PamContext {
    id: fingerprintPam
    config: "omarchy-lock-fingerprint"
    user: root.userName

    onCompleted: function(result) {
      root.handleFingerprintFinished(result)
    }

    onError: function(error) {
      root.fingerprintAuthenticating = false
      if (root.lockRequested && root.fingerprintConfigured) fingerprintRetryTimer.restart()
    }
  }

  Timer {
    id: fingerprintRetryTimer
    interval: 250
    repeat: false
    onTriggered: root.startFingerprint()
  }

  Process {
    id: readlinkProc
    command: ["readlink", "-f", root.currentBackgroundLink]
    stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var next = String(text || "").trim()
        if (next !== root.backgroundPath) {
          root.backgroundPath = next
          root.backgroundVersion += 1
        }
      }
    }
  }

  Process {
    id: fingerprintCheckProc
    command: ["bash", "-c", "if [[ -f /etc/pam.d/omarchy-lock-fingerprint ]] && command -v fprintd-list >/dev/null 2>&1 && fprintd-list \"$USER\" 2>/dev/null | grep -qi finger; then echo yes; else echo no; fi"]
    stdout: StdioCollector { id: fingerprintCheckStdout; waitForEnd: true }
    onExited: {
      root.fingerprintConfigured = String(fingerprintCheckStdout.text || "").trim() === "yes"
      if (root.lockRequested && root.fingerprintConfigured) root.startFingerprint()
      else if (!root.fingerprintConfigured && fingerprintPam.active) fingerprintPam.abort()
    }
  }

  Process {
    id: strandedLockCheckProc
    command: ["bash", "-c", "omarchy-hyprland-session-locked"]
    onExited: function(exitCode) {
      // No output to read the lock off yet.
      if (exitCode === 2) return

      root.strandedLockResolved = true

      // A lock taken while this was in flight is this shell's own.
      root.strandedLock = exitCode === 0 && !root.locked && !root.lockRequested
      root.recoverStrandedLock()
    }
  }

  Process {
    id: wakeProcess
    command: ["bash", "-c", "omarchy-system-wake"]
  }

  Process {
    id: blankProcess
    command: ["bash", "-c", "omarchy-brightness-keyboard off; omarchy-brightness-display off"]
  }

  Timer {
    id: idleBlankTimer
    interval: 5000
    repeat: false
    property double armedAt: 0
    onTriggered: {
      // A countdown frozen by suspend fires right after resume, which would
      // blank the freshly woken unlock screen under the user. Wall-clock time
      // exposes the gap: take a fresh run-up instead of blanking.
      if (Date.now() - armedAt > interval + 2000) {
        root.armBlankTimer()
        return
      }
      // Only a password check in flight should hold the display up. The
      // fingerprint PAM stays armed for the whole lock, so gating on
      // `authenticating` here would keep the panel lit until unlock.
      if (root.lockRequested && !root.authenticatingPassword) root.runBlank()
    }
  }

  Timer {
    id: sessionLockStabilizeTimer
    interval: 500
    repeat: false
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: pendingSessionLockTimer
    interval: 100
    repeat: true
    onTriggered: root.requestSessionLock()
  }

  Timer {
    id: strandedLockRetryTimer
    interval: 500
    repeat: true
    // Covers the compositor settling; screens coming back re-arm it.
    readonly property int budget: 20
    property int remaining: 20
    running: !root.strandedLockResolved && remaining > 0

    function rearm() {
      if (!root.strandedLockResolved) remaining = budget
    }

    onTriggered: {
      remaining -= 1
      root.checkStrandedLock()
    }
  }

  Connections {
    target: Quickshell
    function onScreensChanged() {
      root.requestSessionLock()

      // A monitor still coming up has no workspace, so cannot answer yet.
      strandedLockRetryTimer.rearm()
      root.checkStrandedLock()
    }
  }

  onAuthenticatingPasswordChanged: {
    if (!lockRequested) return
    if (authenticatingPassword) idleBlankTimer.stop()
    else armBlankTimer()
  }

  FileView {
    path: "/etc/pam.d/omarchy-lock-password"
    watchChanges: true
    printErrors: false
    onLoaded: root.passwordPamConfigured = true
    onLoadFailed: root.passwordPamConfigured = false
    onFileChanged: reload()
  }

  // No lock before PAM is known good. An answer from before then may be stale --
  // the failsafe can be cleared from a TTY -- so re-ask rather than act on it.
  onPasswordPamConfiguredChanged: {
    if (!passwordPamConfigured) return

    strandedLock = false
    strandedLockResolved = false
    strandedLockRetryTimer.rearm()
    checkStrandedLock()
  }

  Component.onCompleted: {
    refreshBackground()
    refreshFingerprintStatus()
    checkStrandedLock()
  }

  IpcHandler {
    target: "lock"

    function lock(): string {
      if (!root.passwordPamConfigured) return "missing-pam"
      if (!root.locked && !root.beginLock()) return "failed"
      return "ok"
    }

    function isLocked(): string {
      return root.locked ? "true" : "false"
    }

    function status(): string {
      return JSON.stringify({
        locked: root.locked,
        requested: root.lockRequested,
        pending: root.pendingSessionLock,
        sessionLocked: sessionLock.locked,
        secure: sessionLock.secure,
        realScreens: root.realScreenCount(),
        passwordPam: root.passwordPamConfigured,
        fingerprint: root.fingerprintConfigured,
        authenticating: root.authenticating,
        lastEvent: root.lastEvent,
        lastEventAt: root.lastEventAt
      })
    }

    function preview(): string {
      root.refreshBackground()
      root.refreshFingerprintStatus()
      root.previewVisible = true
      return "ok"
    }

    function hidePreview(): string {
      root.previewVisible = false
      return "ok"
    }

    // Drive the face indicator on the preview surface, so the animation can
    // be seen and tuned without locking: hidden | scanning | success | failure.
    function previewFace(phase: string): string {
      var next = String(phase || "hidden")
      if (["hidden", "scanning", "success", "failure"].indexOf(next) < 0) return "unknown phase"
      root.refreshBackground()
      root.previewVisible = true
      root.faceState = next
      root.faceMessage = ""
      return "ok"
    }
  }
}
